plugins {
	id 'com.diffplug.eclipse.mavencentral'
}

eclipseMavenCentral {
	release eclipseVersion, {
		api 'org.eclipse.equinox.common'
		[
				'org.eclipse.core.jobs',
				'org.eclipse.core.resources',
				'org.eclipse.core.runtime',
				'org.eclipse.equinox.app',
				'org.eclipse.jdt.core',
				'org.eclipse.jface',
				'org.eclipse.osgi',
				'org.eclipse.ui.workbench',
		].each it.&implementation
		useNativesForRunningPlatform()
	}
}

dependencies {
	implementation(
			'org.osgi:org.osgi.core:6.0.0',
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.java'),
			project(':com.ibm.wala.cast.java.ecj'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.ide'),
			project(':com.ibm.wala.util'),
	)
}

final copyEclipsePluginDependencies = tasks.register('copyEclipsePluginDependencies', Sync) {
	[
		'cast.java',
		'cast.java.ecj',
	].each {
		from project(":com.ibm.wala.$it").tasks.named('jar')
	}
	into project.layout.buildDirectory.dir('eclipse-plugin-dependencies')
	rename "(.*)-${java.util.regex.Pattern.quote(version)}(.*)", '$1$2'
}

eclipse {
	synchronizationTasks copyEclipsePluginDependencies
}

tasks.register('prepareMavenBuild') {
	dependsOn copyEclipsePluginDependencies
}

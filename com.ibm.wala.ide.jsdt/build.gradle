plugins {
	id 'com.diffplug.eclipse.mavencentral'
}

repositories {
	maven {
		url 'https://artifacts.alfresco.com/nexus/content/repositories/public/'
		content {
			includeGroup 'org.eclipse.wst.jsdt'
		}
	}
}

eclipseMavenCentral {
	release eclipseVersion, {
		[
				'org.eclipse.core.jobs',
				'org.eclipse.core.resources',
				'org.eclipse.core.runtime',
				'org.eclipse.equinox.common',
				'org.eclipse.osgi',
				'org.eclipse.ui.workbench',
		].each it.&implementation
		useNativesForRunningPlatform()
	}
}

configurations {
	eclipsePlugin
}

dependencies {
	api(project(':com.ibm.wala.ide')) {
		because 'public class JavaScriptHeadlessUtil extends class HeadlessUtil'
	}
	implementation(
			"org.eclipse.wst.jsdt:core:$eclipseWstJsdtVersion",
			"org.eclipse.wst.jsdt:ui:$eclipseWstJsdtVersion",
			project(':com.ibm.wala.cast'),
			project(':com.ibm.wala.cast.js'),
			project(':com.ibm.wala.cast.js.rhino'),
			project(':com.ibm.wala.core'),
			project(':com.ibm.wala.util'),
	)
	eclipsePlugin(
			'net.htmlparser.jericho:jericho-html:3.2',
			'org.mozilla:rhino:1.7.12',
	)
}

final copyEclipsePluginDependencies = tasks.register('copyEclipsePluginDependencies', Sync) {
	[
		'cast.js',
		'cast.js.rhino',
	].each {
		from project(":com.ibm.wala.$it").tasks.named('jar')
	}
	from configurations.eclipsePlugin
	into project.layout.buildDirectory.dir('eclipse-plugin-dependencies')
	rename '(.*)-(?:[0-9.]+)(?:-SNAPSHOT)?(\\.jar)', '$1$2'
}

eclipse {
	synchronizationTasks copyEclipsePluginDependencies
}

tasks.register('prepareMavenBuild') {
	dependsOn copyEclipsePluginDependencies
}

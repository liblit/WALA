apply plugin: 'com.github.hauner.jarTest'
apply plugin: 'eclipse'

eclipse.project.natures 'org.eclipse.pde.PluginNature'

sourceSets.test {
	java.srcDirs = ['src']
	resources.srcDirs = ['dat']
}

dependencies {
	testCompile(
		'junit:junit:4.11',
		'org.apache.ant:ant:1.8.2',
		'org.eclipse.core:org.eclipse.core.runtime:3.10.0.v20140318-2214',
		'org.hamcrest:hamcrest-core:1.3',
		'org.osgi:org.osgi.core:4.2.0',
		project(':com.ibm.wala.core'),
		project(':com.ibm.wala.shrike'),
		project(':com.ibm.wala.util'),
		)
}

processTestResources {
	def testdata = project(':com.ibm.wala.core.testdata')
	dependsOn testdata.verifyJavaCup
	// TODO: something of what follows ends up copying the entire
	// 'com.ibm.wala.core.testdata' directory!
	from testdata.collectJLex
	from testdata.collectTestData
	from testdata.downloadJavaCup
	from testdata.extractBcel
	from testdata.generateHelloHash
}

test {
	maxHeapSize = '800M'
	systemProperty 'com.ibm.wala.junit.analyzingJar', 'true'
	systemProperty 'com.ibm.wala.junit.profile', 'short'

	// https://github.com/liblit/WALA/issues/5
	exclude '**/DynamicCallGraphTest.class'
	exclude '**/GetTargetsTest.class'
	exclude '**/PruneArrayOutOfBoundExceptionEdge.class'
}

task cleanTest(type: Delete) {
	delete 'report'
}

clean.dependsOn cleanTest
